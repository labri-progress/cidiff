plugins {
    id 'application'
    id 'checkstyle'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

compileJava {
    options.release = 17
}

application {
    mainClass = 'org.github.gumtreediff.cidiff.CiDiff'
    distTar.enabled = false
}

pitest {
    junit5PluginVersion = '0.15'
    targetClasses = ['org.github.gumtreediff.cidiff.*']
}

tasks.named('test') {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
        events "passed", "skipped", "failed"
    }
}

task runBenchmark(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.github.gumtreediff.cidiff.benchmarks.PrecisionBenchmark'
    args "${rootProject.projectDir}/data/breakages/", "${rootProject.projectDir}/benchmark/precision_recall.csv"
}


build.finalizedBy installDist